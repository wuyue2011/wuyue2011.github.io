import org.apache.tools.ant.filters.ReplaceTokens

import java.time.Instant

plugins {
	id "architectury-plugin" version "3.4.160"
	id "dev.architectury.loom" version "1.9.424" apply false
	id "de.undercouch.download" version "4.1.2"
	id "com.github.johnrengelman.shadow" version "7.1.0"
	id "io.github.pacifistmc.forgix" version "1.2.9"
	id "base"
}

apply from: "build_dep_ver.gradle"

rootProject.ext.assetExcludeFont = rootProject.properties.containsKey("assetExcludeFont")

architectury {
	minecraft = minecraft_version
}

configure(subprojects.findAll {it.name != 'rhino'}) {
	apply plugin: "dev.architectury.loom"

	loom {
		silentMojangMappingsLicense()
	}

	configurations {
		shadowCommon
	}

	dependencies {
		modApi("me.shedaniel.cloth:basic-math:+")
		annotationProcessor 'systems.manifold:manifold-preprocessor:+'
		minecraft "com.mojang:minecraft:${minecraft_version}"
		mappings parchment_version == "" ? loom.officialMojangMappings() : loom.layered() {
			officialMojangMappings()
			parchment("org.parchmentmc.data:parchment-${minecraft_version}:${parchment_version}@zip")
		}
		implementation files("../checkouts/mtr-common.jar")
		implementation "org.msgpack:msgpack-core:${rootProject.messagepack_version}"

		implementation project(":rhino")
		shadowCommon project(":rhino")
	}

	build.finalizedBy(mergeJars)
	assemble.finalizedBy(mergeJars)
}

task setupLibrary() {

	String url = "https://aphrodite281.github.io/Minecraft-Transit-Railway/lib/" + minecraft_version + "/"

	doFirst {
		download {
			src "${url}mtr-common.jar"
			dest "checkouts/mtr-common.jar"
			overwrite true
		}

		download {
			src "${url}mtr-fabric.jar"
			dest "checkouts/mtr-fabric.jar"
			overwrite true
		}

		download {
			src "${url}mtr-forge.jar"
			dest "checkouts/mtr-forge.jar"
			overwrite true
		}
		download {
			src "${url}files.zip"
			dest "checkouts/files.zip"
			overwrite true
		}
	}
}

build {
	doFirst {
		copy {
			// outputs.upToDateWhen { false }
			from(zipTree("checkouts/files.zip")) { eachFile { file -> file.relativePath = new RelativePath(true, file.relativePath.segments.drop(1) as String[]) } }
			into "common/src/main/java/cn/zbx1425/mtrsteamloco/mappings"
			filter(ReplaceTokens, tokens: ["package": "cn.zbx1425.mtrsteamloco.mappings; import mtr.mappings.*"])
		}

		ant.path { ant.fileset(dir: "common/src/main/java/cn/zbx1425/mtrsteamloco/mappings", includes: "Fabric*.java") }.list().each {
			ant.move(file: it, todir: "fabric/src/main/java/cn/zbx1425/mtrsteamloco/mappings")
		}

		ant.path { ant.fileset(dir: "common/src/main/java/cn/zbx1425/mtrsteamloco/mappings", includes: "Forge*.java") }.list().each {
			ant.move(file: it, todir: "forge/src/main/java/cn/zbx1425/mtrsteamloco/mappings")
		}

		ant.delete(dir: "common/src/main/java/cn/zbx1425/mtrsteamloco/mappings")

		copy {
			// outputs.upToDateWhen { false }
			from "checkouts/mtr-fabric.jar"
			into "fabric/run/mods"
		}

		copy {
			// outputs.upToDateWhen { false }
			from "checkouts/mtr-forge.jar"
			into "forge/run/mods"
		}
	}
}

task setupFiles() {
	copy {
		outputs.upToDateWhen { false }
		from "common/src/main/BuildConfigTemplate.java"
		into "common/src/main/java/cn/zbx1425/mtrsteamloco"
		filter(ReplaceTokens, tokens: [
				"version": rootProject.mod_version + "+" + minecraft_version,
				"protocol_version": rootProject.mod_protocol_version,
				"build_time": Instant.now().epochSecond.toString()
		])
		rename "(.+)Template.java", "\$1.java"
	}
}

configure(allprojects.findAll {it.name != 'rhino'}) {
	apply plugin: "architectury-plugin"

	version = rootProject.mod_version + "+" + minecraft_version
	group = project.maven_group

	repositories {
		maven { url = "https://maven.terraformersmc.com/" }
		maven { url = "https://maven.parchmentmc.org/" }
		maven { url "https://maven.shedaniel.me"}
	}

	tasks.withType(JavaCompile) {
		options.encoding = "UTF-8"
    	options.incremental = true;
		

		if (minecraft_main_version == 16) {
			def targetVersion = 8
			if (JavaVersion.current().isJava9Compatible()) {
				options.release.set(targetVersion)
			}
		} else if (minecraft_main_version == 17) {
			options.release.set(16)
		} else {
			options.release.set(17)
		}

        options.compilerArgs += ['-Xplugin:Manifold', '-AMC_VERSION=' + minecraft_version_int]
	}

	afterEvaluate {
		for (def task in it.tasks) {
			if (task != rootProject.tasks.setupFiles) {
				task.dependsOn rootProject.tasks.setupFiles
			}
		}
		if (System.env.GITHUB_ACTIONS) {
			rootProject.tasks.build.dependsOn rootProject.tasks.setupLibrary
		}
	}
}

forgix {
	group = "cn.zbx1425"
	mergedJarName = "MTR-ANTE" + "-" + rootProject.mod_version + "+" + minecraft_version + ".jar"
	outputDir = "build/"

	forge {
		projectName = "forge"
		jarLocation = "build/libs/forge" + "-" + rootProject.mod_version + "+" + minecraft_version + ".jar"
	}

	fabric {
		projectName = "fabric"
		jarLocation = "build/libs/fabric" + "-" + rootProject.mod_version + "+" + minecraft_version + ".jar"
	}
}

import org.gradle.api.internal.file.copy.CopyAction;
import org.gradle.api.internal.file.copy.CopyActionProcessingStream;

class DummyArchiveTask extends AbstractArchiveTask {

	protected CopyAction createCopyAction() {
		return new DummyCopyAction();
	}

	class DummyCopyAction implements CopyAction {

		@Override
		WorkResult execute(CopyActionProcessingStream stream) {
			return WorkResults.didWork(true);
		}
	}
}

tasks.register('dummyForgixOutput', DummyArchiveTask) {
	// Forgix's mergeJars task doesn't extend AbstractArchiveTask.
	// This is to make github.com/teaconmc/longjing happy.
	destinationDirectory = file("build/")
	archiveBaseName = "MTR-ANTE"
	archiveVersion = rootProject.mod_version + "+" + minecraft_version
	archiveExtension = "jar"
}

tasks.withType(JavaCompile) {
    outputs.cacheIf { true }
    options.incremental = true;
}