plugins {
	id "com.github.johnrengelman.shadow"
}

loom {
	accessWidenerPath = project(":common").loom.accessWidenerPath

	forge {
		mixinConfig "mtrsteamloco.mixins.json"

		convertAccessWideners = true
		extraAccessWideners.add loom.accessWidenerPath.get().asFile.name
	}
}

architectury {
	platformSetupLoomIde()
	forge()
}

configurations {
	common
	shadowCommon
	clothConfigJar
	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common
	developmentForge.extendsFrom common
}

dependencies {
	// implementation "com.github.stuxuhai:jpinyin:1.1.7"
	shadowCommon("com.github.stuxuhai:jpinyin:1.1.7")
	forge "net.minecraftforge:forge:${rootProject.forge_version}"
	modApi "${rootProject.architectury_id}:architectury-forge:${rootProject.architectury_version}"

	common(project(path: ":common", configuration: "namedElements")) { transitive false }
	shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive = false }

	forgeRuntimeLibrary project(":rhino")

	modApi("me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_config_version}") { transitive false }
	modApi("me.shedaniel.cloth:basic-math:0.6.1") { transitive false }
	clothConfigJar("me.shedaniel.cloth:cloth-config-forge:${rootProject.cloth_config_version}") { transitive = false }
	clothConfigJar("me.shedaniel.cloth:basic-math:0.6.1") { transitive false }
}

processResources {
	inputs.property "version", project.version

	filesMatching("META-INF/mods.toml") {
		expand (
			"version": project.version,
			"minecraft_version_range": rootProject.supports_minecraft_version_range,
			"mtr_min_version": rootProject.mtr_min_version
		)
	}
}

shadowJar {
	exclude "fabric.mod.json"
	exclude "*.bmp"
	exclude '**/*.kotlin_metadata'
    exclude '**/*.kotlin_module'
    exclude '**/*.kotlin_builtins'

	if (rootProject.ext.assetExcludeFont) {
		exclude "assets/mtr/font/**"
	}

	relocate 'de.javagl', 'vendor.cn.zbx1425.sowcerext.de.javagl'
	relocate 'com.github.stuxuhai', 'vendor.cn.zbx1425.mtrsteamloco.com.github.stuxuhai'
	// relocate 'org.mozilla', 'vendor.cn.zbx1425.mtrsteamloco.org.mozilla'

	from project.configurations.clothConfigJar.files.collect {
		return zipTree(it).matching { filter ->
			filter.include { e ->
				matchInnerClass e.path as String,
						"me/shedaniel/clothconfig2",
						"me/shedaniel/math",
						"assets/cloth-config2"
			}
		}
	}

	relocate "me.shedaniel", "cn.zbx1425.mtrsteamloco.vendor.me.shedaniel"


	configurations = [project.configurations.shadowCommon]
	archiveClassifier = "dev-shadow"
}

static boolean matchInnerClass(String path, String... names) {
	names.any { name -> path.startsWith(name) }
}

remapJar {
	input.set shadowJar.archiveFile
	dependsOn shadowJar
	archiveClassifier = null
}

components.java {
	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
		skip()
	}
}

task moveForgeBuilds() {
	doLast {
		delete fileTree("../build").matching {
			include "**/MTR-ANTE-forge*"
		}
		copy {
			from "build/libs"
			into "../build"
			exclude "*dev-shadow.jar"
			rename "(.+)", "MTR-ANTE-\$1"
		}
	}
}

afterEvaluate {
	for (def task in it.tasks) {
		if (task == tasks.build) {
			// task.finalizedBy tasks.moveForgeBuilds
			if (System.env.GITHUB_ACTIONS) {
				task.dependsOn rootProject.tasks.setupLibrary
			}
		}
	}
}
